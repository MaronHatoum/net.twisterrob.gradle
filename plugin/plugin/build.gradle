apply plugin: 'groovy'
apply plugin: 'kotlin'
apply plugin: 'net.twisterrob.gradle.test'
version = '3.1.3.14-27-27.0'

dependencies { // last checked 2018-06-09 (all latest)
	implementation gradleApi()
	implementation 'org.tmatesoft.svnkit:svnkit:1.9.3' // https://mvnrepository.com/artifact/org.tmatesoft.svnkit/svnkit
	implementation 'org.tmatesoft.svnkit:svnkit-cli:1.9.3'
	implementation 'org.ajoberstar:gradle-git:1.7.2' // https://github.com/ajoberstar/gradle-git/releases
	implementation 'com.android.tools.build:gradle:3.1.3' // https://developer.android.com/studio/releases/gradle-plugin.html#updating-gradle
	implementation 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.41'
	implementation ('org.gradle:gradle-kotlin-dsl:0.17.5') {
		transitive = false // make sure to not pull in kotlin-compiler-embeddable
	}
}
configurations.all {
	resolutionStrategy.eachDependency { DependencyResolveDetails dep ->
		if (dep.requested.group == 'org.jetbrains.kotlin' && dep.requested.name == 'kotlin-stdlib-jre7') {
			dep.useTarget("${dep.target.group}:kotlin-stdlib-jdk7:${dep.target.version}")
			dep.because 'https://issuetracker.google.com/issues/72274424'
		}
		if (dep.requested.group == 'org.jetbrains.kotlin' && dep.requested.name == 'kotlin-stdlib-jre8') {
			dep.useTarget("${dep.target.group}:kotlin-stdlib-jdk8:${dep.target.version}")
			dep.because 'https://issuetracker.google.com/issues/72274424'
		}
	}
}

dependencies { // test
	testImplementation "junit:junit:4.12"
	testImplementation "org.hamcrest:java-hamcrest:2.0.0.0"
	testImplementation "org.hamcrest:hamcrest-junit:2.0.0.0"
	testImplementation "org.jetbrains:annotations:16.0.2"
	testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.41"
	testImplementation "org.jetbrains.kotlin:kotlin-test-junit:1.2.41"
	testImplementation "com.jakewharton.dex:dex-method-list:3.1.0"
}

project.with {
	sourceCompatibility = 1.7
	targetCompatibility = 1.7
}

// TODEL remove when everything is Kotlin
afterEvaluate {
	compileGroovy.dependsOn.remove('compileJava')
	compileJava.dependsOn.remove(compileKotlin)
	compileKotlin.dependsOn(compileGroovy)
	compileKotlin.classpath += files(compileGroovy.destinationDir)
}

jar {
	manifest {
		it.attributes(
				"Implementation-Vendor": project.group,
				"Implementation-Title": project.name,
				"Implementation-Version": project.version,
				// parsed in net.twisterrob.gradle.common.BasePlugin.getBuiltDate
				"Built-Date": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		)
	}
}
