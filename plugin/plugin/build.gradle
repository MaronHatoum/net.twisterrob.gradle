apply plugin: 'kotlin'
apply plugin: 'net.twisterrob.gradle.test'
version = '3.1.4.14-28-28.1'

dependencies { // last checked 2018-06-09 (all latest)
	implementation gradleApi()
	// https://mvnrepository.com/artifact/org.tmatesoft.svnkit/svnkit
	implementation 'org.tmatesoft.svnkit:svnkit:1.9.3'
	implementation 'org.tmatesoft.svnkit:svnkit-cli:1.9.3'
	// https://github.com/ajoberstar/gradle-git/releases
	implementation 'org.ajoberstar:gradle-git:1.7.2'
	// https://developer.android.com/studio/releases/gradle-plugin.html#updating-gradle
	implementation 'com.android.tools.build:gradle:3.1.4'
	implementation 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.71'
	implementation('org.gradle:gradle-kotlin-dsl:1.0-rc-11') {
		transitive = false // make sure to not pull in kotlin-compiler-embeddable
	}
}
configurations.all {
	resolutionStrategy.eachDependency { DependencyResolveDetails dep ->
		if (dep.requested.group == 'org.jetbrains.kotlin' && dep.requested.name == 'kotlin-stdlib-jre7') {
			dep.useTarget("${dep.target.group}:kotlin-stdlib-jdk7:${dep.target.version}")
			dep.because 'https://issuetracker.google.com/issues/72274424'
		}
		if (dep.requested.group == 'org.jetbrains.kotlin' && dep.requested.name == 'kotlin-stdlib-jre8') {
			dep.useTarget("${dep.target.group}:kotlin-stdlib-jdk8:${dep.target.version}")
			dep.because 'https://issuetracker.google.com/issues/72274424'
		}
	}
}

dependencies { // test
	testImplementation "junit:junit:4.12"
	testImplementation "org.hamcrest:java-hamcrest:2.0.0.0"
	testImplementation "org.hamcrest:hamcrest-junit:2.0.0.0"
	testImplementation "org.jetbrains:annotations:16.0.2"
	testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.71"
	testImplementation "org.jetbrains.kotlin:kotlin-test-junit:1.2.71"
	testImplementation "com.jakewharton.dex:dex-member-list:3.2.1"
}

jar {
	manifest {
		//noinspection UnnecessaryQualifiedReference
		attributes(
				"Implementation-Vendor": project.group,
				"Implementation-Title": project.name,
				"Implementation-Version": project.version,
				// parsed in net.twisterrob.gradle.builtDate (Global.kt)
				"Built-Date": java.time.format.DateTimeFormatter.ISO_INSTANT.format(java.time.Instant.now())
		)
	}
}

// Enable verbose test logging, because sometimes AndroidBuildPluginIntgTest hangs, hopefully this will uncover it
tasks.withType(Test) {
	//noinspection UnnecessaryQualifiedReference
	testLogging.events = org.gradle.api.tasks.testing.logging.TestLogEvent.values().toList().toSet()
}
