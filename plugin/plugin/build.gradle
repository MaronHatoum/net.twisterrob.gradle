apply plugin: 'kotlin'
apply plugin: 'net.twisterrob.gradle.test'
version = '3.6.4.14-29-29.0'

dependencies { // last checked 2019-05-27 (all latest, except Gradle+Kotlin)
	implementation gradleApi()
	// https://mvnrepository.com/artifact/org.tmatesoft.svnkit/svnkit
	implementation 'org.tmatesoft.svnkit:svnkit:1.9.3'
	implementation 'org.tmatesoft.svnkit:svnkit-cli:1.9.3'
	// https://github.com/ajoberstar/gradle-git/releases
	implementation 'org.ajoberstar:gradle-git:1.7.2'
	// TODEL explicit dependencies are required because https://github.com/gradle/gradle/issues/11875
	implementation /*+--- */ 'org.ajoberstar:grgit:1.9.3'
	implementation /*|    +--- */ 'org.eclipse.jgit:org.eclipse.jgit:4.5.2.201704071617-r'
	// https://developer.android.com/studio/releases/gradle-plugin.html#updating-gradle
	implementation 'com.android.tools.build:gradle:3.5.4'
	implementation 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72'
	implementation('org.gradle:gradle-kotlin-dsl:5.6.4') {
		transitive = false // make sure to not pull in kotlin-compiler-embeddable
	}
}
configurations.all {
	resolutionStrategy.eachDependency { DependencyResolveDetails dep ->
		if (dep.requested.group == 'org.jetbrains.kotlin' && dep.requested.name == 'kotlin-stdlib-jre7') {
			dep.useTarget("${dep.target.group}:kotlin-stdlib-jdk7:${dep.target.version}")
			dep.because 'https://issuetracker.google.com/issues/72274424'
		}
		if (dep.requested.group == 'org.jetbrains.kotlin' && dep.requested.name == 'kotlin-stdlib-jre8') {
			dep.useTarget("${dep.target.group}:kotlin-stdlib-jdk8:${dep.target.version}")
			dep.because 'https://issuetracker.google.com/issues/72274424'
		}
		// https://github.com/junit-team/junit4/pull/1608#issuecomment-496238766
		if (dep.requested.group == 'org.hamcrest') {
			switch (dep.requested.name) {
				case 'java-hamcrest':
					dep.useTarget("org.hamcrest:hamcrest:${dep.target.version}")
					dep.because("2.0.0.0 shouldn't have been published")
					break
				case 'hamcrest-core':
					dep.useTarget("org.hamcrest:hamcrest:${dep.target.version}")
					dep.because("hamcrest-core doesn't contain anything")
					break
				case 'hamcrest-library':
					dep.useTarget("org.hamcrest:hamcrest:${dep.target.version}")
					dep.because("hamcrest-library doesn't contain anything")
					break
			}
		}
	}
}

dependencies { // test
	testImplementation "junit:junit:4.13"
	testImplementation "org.hamcrest:hamcrest:2.1"
	testImplementation "org.hamcrest:hamcrest-junit:2.0.0.0"

	testImplementation "org.jetbrains:annotations:17.0.0"
	testImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.72"
	testImplementation "org.jetbrains.kotlin:kotlin-test-junit:1.3.72"
	testImplementation("com.jakewharton.dex:dex-member-list:4.1.1") {
		exclude group: 'org.jetbrains.kotlin'
	}
}

jar {
	manifest {
		//noinspection UnnecessaryQualifiedReference
		attributes(
				"Implementation-Vendor": project.group,
				"Implementation-Title": project.name,
				"Implementation-Version": project.version,
				// parsed in net.twisterrob.gradle.builtDate (Global.kt)
				"Built-Date": java.time.format.DateTimeFormatter.ISO_INSTANT.format(java.time.Instant.now())
		)
	}
}

tasks.withType(Test) {
	// Enable verbose test logging, because sometimes AndroidBuildPluginIntgTest hangs, hopefully this will uncover it
	//noinspection UnnecessaryQualifiedReference
	testLogging.events = org.gradle.api.tasks.testing.logging.TestLogEvent.values().toList().toSet()
	// See GradleTestKitDirRelocator for what enables this!
	maxParallelForks = 10
	// Limit memory usage of test forks. Gradle <5 allows 1/4th of total memory to be used, thus forbidding many forks.
	maxHeapSize = "256M"
}
