group = rootProject.name

buildscript {
	repositories {
		jcenter()
		maven { name = "TWiStErRob Bintray"; url = "https://dl.bintray.com/twisterrob/maven" }
	}
	dependencies {
		classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.31'
		classpath 'net.twisterrob.gradle:twister-gradle-test:0.9'
	}
}

subprojects {
	repositories {
		jcenter()
		google()
		maven { name = "TWiStErRob Bintray"; url = "https://dl.bintray.com/twisterrob/maven" }
		maven { name = "Gradle libs (for Kotlin-DSL)"; url = "https://repo.gradle.org/gradle/libs-releases-local/" }
	}

	group = rootProject.group
	apply from: 'http://localhost/maven/configure.gradle'
}

allprojects {
	gradle.projectsEvaluated {
		def java_opts = [
				// check everything
				"-Xlint:all",
				// fail on any warning
				"-Werror",
				//warning: [options] bootstrap class path not set in conjunction with -source 1.7
				"-Xlint:-options",
				//The following annotation processors were detected on the compile classpath:
				// 'javaslang.match.PatternsProcessor'
				// 'com.google.auto.value.extension.memoized.MemoizedValidator'
				// 'com.google.auto.value.processor.AutoAnnotationProcessor'
				// 'com.google.auto.value.processor.AutoValueBuilderProcessor'
				// 'com.google.auto.value.processor.AutoValueProcessor'.
				// implies "-Xlint:-processing",
				"-proc:none"
		]
		tasks.withType(JavaCompile) {
			it.options.compilerArgs += java_opts
		}
		tasks.withType(GroovyCompile) {
			it.options.compilerArgs += java_opts
			it.options.compilerArgs += [
					//warning: Implicitly compiled files were not subject to annotation processing.
					//Use -implicit to specify a policy for implicit compilation.
					"-implicit:class"
			]
		}
	}
}
